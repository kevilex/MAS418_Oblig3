<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_AutomaticControl" Id="{b543be1d-46f1-4ea5-ae35-9d7ea7728984}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_AutomaticControl
VAR_INPUT
	eMode 								: E_Mode;
	eStatus 							: E_Status;
	bStopMotion 						: BOOL;
	stMotionReferenceGeneratorSettings  : ST_MotionReferenceGeneratorSettings;
	stAutomaticControlSettings 			: ST_AutomaticControlSettings;
	stParam 							: ST_Parameters; 
	fPositionFeedback				 : LREAL;
END_VAR
VAR_OUTPUT
	fTime : LREAL;
	fControlOutputNormalized : LREAL;
END_VAR
VAR
	fbClock : FB_Clock;	
	fbMotionReferenceGenerator 		: FB_MotionReferenceGenerator_limit;	
	fbVelocityFeedForwardControl 	: FB_OpenLoopControl;
	fbResetIntegralController 		: FB_ResetIntegralController; 
	fbClosedLoopPositionControl 	: FB_ClosedLoopControl;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[fbClock(
	bStart := (eMode = E_Mode.Auto AND (eStatus = E_Status.Starting OR eStatus = E_Status.Running)),
	bStop := bStopMotion,
	fCycleTime := stParam.fCyclicTime
);

fbMotionReferenceGenerator(
	bEnable := (eMode = E_Mode.Auto AND eStatus = E_Status.Running),
	stMotionReferenceGeneratorSettings := stMotionReferenceGeneratorSettings,
	fClock_s := fbClock.fTime
);

fbVelocityFeedForwardControl(
	bEnable := stAutomaticControlSettings.bEnableVelocityFeedForwardControl,
	stParam := stParam,
	fInput_ms := fbMotionReferenceGenerator.fVelocityReference_ms
);

fbResetIntegralController(
	stParam := stParam,
	fPositionError_m := fbClosedLoopPositionControl.fPositionError,
	fVelocityReference_ms := fbMotionReferenceGenerator.fVelocityReference_ms
);

fbClosedLoopPositionControl(
	bEnable := stAutomaticControlSettings.bEnableClosedLoopPositionControl,
	stParam := stParam,
	bActivateIntegralController := stAutomaticControlSettings.bEnableIntegralController,
	bIntegralControllReset := fbResetIntegralController.bReset,
	fInput_m := G_Commissioning.SetPointVar,
	fFeedback_m := fPositionFeedback
);

IF (eMode = E_Mode.Auto AND eStatus = E_Status.Running) THEN
	fControlOutputNormalized := LIMIT(-1.0, (fbVelocityFeedForwardControl.fNormalizedOutput + fbClosedLoopPositionControl.fNormalizedOutput), 1.0);
ELSE
	fControlOutputNormalized := 0.0;
END_IF]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>